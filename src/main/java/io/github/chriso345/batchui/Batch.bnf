{
    parserClass = "io.github.chriso345.batchui.parser.BatchParser"

    extends = "com.intellij.extapi.psi.ASTWrapperPsiElement"

    psiClassPrefix = "Batch"
    psiImplClassSuffix = "Impl"
    psiPackage = "io.github.chriso345.batchui.psi"
    psiImplPackage = "io.github.chriso345.batchui.psi.impl"

    elementTypeHolderClass = "io.github.chriso345.batchui.psi.BatchTypes"
    elementTypeClass = "io.github.chriso345.batchui.psi.BatchElementType"
    tokenTypeClass = "io.github.chriso345.batchui.psi.BatchTokenType"

    psiImplUtilClass = "io.github.chriso345.batchui.psi.impl.BatchPsiImplUtil"
}

batchFile ::= item_*

// May be needed for reference completions later??
label ::= (LABEL_MARKER? FUNC_LABEL) {
    methods = [
        getLabelMarker
        getFuncLabel
    ]
}

//cmd_terminator ::= (PIPE|AND|LESS|GREATER|DOUBLE_PIPE|DOUBLE_AND|DOUBLE_LESS|DOUBLE_GREATER)

types_ ::= STRING | NUMERIC | PLAINTEXT | DISK_DRIVE
operators ::= COMPARISON_OPERATOR | OPERATOR

private item_ ::= (
    prefix          // ECHO, SET, etc
    | label
    | COMMENT        // REM, ::
    | CRLF
)

prefix ::= (DECORATOR? command)

command ::= command_reference | annotation_reference | COMMENT_REFERENCE | PLAINTEXT

command_reference ::= (
    ASSOC_COMMAND assoc_arguments?
    | CALL_COMMAND call_arguments?
    | ECHO_COMMAND echo_arguments?
    | ENDLOCAL_COMMAND
    | EXIT_COMMAND exit_arguments?
    | GOTO_COMMAND label?
    | IF_COMMAND if_arguments?
    | MORE_COMMAND more_arguments?
    | SET_COMMAND set_arguments?
    | SETLOCAL_COMMAND setlocal_arguments?
    | SHIFT_COMMAND shift_arguments?
) (COMMAND_TERMINATOR command)?

annotation_reference ::= (
    REM_DECORATOR COMMENT?
    | ECHO_ANNOTATION echo_arguments?
) (COMMAND_TERMINATOR command)?

assoc_arguments ::= VARIABLE (ASSIGNMENT (STRING|NUMERIC)?)?
call_arguments ::= label? // todo: add extra arguments
echo_arguments ::= (TOGGLE | (STRING|PLAINTEXT)*)?
exit_arguments ::= EXTENSION NUMERIC?
more_arguments ::= (EXTENSION | PLAINTEXT | DISK_DRIVE) more_arguments?
set_arguments ::= EXTENSION? VARIABLE ASSIGNMENT (STRING | types_)?
setlocal_arguments ::= SETLOCAL_PARAMETER SETLOCAL_PARAMETER?
shift_arguments ::= EXTENSION

if_arguments ::= COMPARISON_OPERATOR? (error_if | standard_if | exist_if)

// TODO: brackets logic
error_if ::= ERRORLEVEL_TOKEN NUMERIC command
standard_if ::= (STRING | VARIABLE) operators (STRING | VARIABLE) command
exist_if ::= EXIST_TOKEN STRING command
